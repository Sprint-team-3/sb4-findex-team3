plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

def queryDslVersion = '5.0.0'
def querydslDir = "build/generated/querydsl"

repositories {
	mavenCentral()
}

dependencies {
	// === Spring Boot Starters ===
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// === Lombok ===
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// === DB Drivers ===
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'com.h2database:h2'

	// === QueryDSL ===
	implementation "com.querydsl:querydsl-core:${queryDslVersion}"
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
	annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jakarta"
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api:3.1.0'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api:2.1.1'

	// === MapStruct ===
	implementation 'org.mapstruct:mapstruct:1.5.3.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

	// === Swagger ===
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4'

	// === CSV ===
	implementation 'com.opencsv:opencsv:5.6'

	// === 테스트 ===
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// === 코드 포맷 ===
//spotless {
//	java {
//		googleJavaFormat()
//	}
//}

// === Q파일 위치 설정 ===
sourceSets {
	main.java.srcDirs += querydslDir
}

tasks.withType(JavaCompile).configureEach {
	options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

tasks.named('clean') {
	delete file(querydslDir)
}

tasks.named('test') {
	useJUnitPlatform()
}